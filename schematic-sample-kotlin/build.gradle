apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'checkstyle'

buildscript {
  repositories {
    google()
    jcenter()
  }

  dependencies {
    classpath parent.libraries.kotlinPlugin
    classpath parent.libraries.androidGradle
  }
}

dependencies {
  compile parent.libraries.kotlinRuntime
  kapt project(':schematic-compiler')
  compile project(':schematic')
  compile parent.libraries.appcompat
  compile parent.libraries.recyclerview
}

android {
  compileSdkVersion parent.ext.compileSdkVersion
  buildToolsVersion parent.ext.buildToolsVersion

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  defaultConfig {
    minSdkVersion parent.ext.minSdkVersion
    targetSdkVersion parent.ext.targetSdkVersion

    versionCode = parent.ext.versionCode;
    versionName = parent.ext.versionName

    buildConfigField "String", "PROVIDER_AUTHORITY",
        "\"net.simonvt.schematic.sample.kotlin.provider.NotesProvider\""
    resValue "string", "authority_provider",
        "net.simonvt.schematic.sample.kotlin.provider.NotesProvider"
  }

  lintOptions {
    abortOnError false
  }

  packagingOptions {
    exclude 'META-INF/services/javax.annotation.processing.Processor'
  }
}

checkstyle {
  configFile project.file('../config/checkstyle/checkstyle.xml')
  showViolations true
}

android.applicationVariants.all { variant ->
  def name = variant.buildType.name

  def checkstyle = project.tasks.create "checkstyle${name.capitalize()}", Checkstyle
  checkstyle.dependsOn variant.javaCompile
  checkstyle.source variant.javaCompile.source
  checkstyle.classpath = project.fileTree(variant.javaCompile.destinationDir)
  checkstyle.exclude('**/BuildConfig.java')
  checkstyle.exclude('**/R.java')
  project.tasks.getByName("check").dependsOn checkstyle
}

// Workaround because compiler and sample is in the same gradle project.
// DO NOT USE WHEN PULLING FROM MAVEN REPO
android.applicationVariants.all { variant -> variant.javaCompile.dependsOn ':schematic-compiler:jar'
}
